buildscript {
    repositories {
        jcenter() // this applies only to the Gradle 'Shadow' plugin
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    }
}

plugins {
    id 'java'
    id 'application'
    // shadow plugin to produce fat JARs
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}


// artifact properties
group = 'com.example.experimentflinkcdcconnectorspostgresdatastream'
version = '0.1-SNAPSHOT'

// Set this to the class you'd like to run
mainClassName = 'com.example.experimentflinkcdcconnectorspostgresdatastream.StreamingJobWithCustomDeserializer'
description = """Flink Quickstart Job"""

ext {
    javaVersion = '1.8'
    flinkVersion = '1.12.0'
    scalaBinaryVersion = '2.11'
    slf4jVersion = '1.7.7'
    log4jVersion = '1.2.17'
}


sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

applicationDefaultJvmArgs = ["-Dlog4j.configuration=log4j.properties"]

// task wrapper(type: Wrapper) {
//     gradleVersion = '3.1'
// }

// declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// -> Explicitly define the // libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar

    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'log4j'
}

// declare the dependencies for your production and test code
dependencies {
    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    compile "org.apache.flink:flink-java:${flinkVersion}"
    compile "org.apache.flink:flink-streaming-java_${scalaBinaryVersion}:${flinkVersion}"

    // for table API
    compile "org.apache.flink:flink-table-api-java-bridge_${scalaBinaryVersion}:${flinkVersion}"
    compile "org.apache.flink:flink-table-planner-blink_${scalaBinaryVersion}:${flinkVersion}"

    // Add this to run your Flink program locally
    // https://ci.apache.org/projects/flink/flink-docs-stable/dev/local_execution.html
    compile "org.apache.flink:flink-clients_${scalaBinaryVersion}:${flinkVersion}"
    // compile "org.apache.flink:flink-runtime_${scalaBinaryVersion}:${flinkVersion}"

    // Add this if you'd like to bring up the web ui at localhost:8081 via
    // StreamExecutionEnvironment.createLocalEnvironmentWithWebUI
    compile "org.apache.flink:flink-runtime-web_${scalaBinaryVersion}:${flinkVersion}"

    compile "log4j:log4j:${log4jVersion}"
    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"

    // Add test dependencies here.
    // testCompile "junit:junit:4.12"

    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------
    flinkShadowJar "com.alibaba.ververica:flink-connector-postgres-cdc:1.4.0"

    // Pulsar
    flinkShadowJar "io.streamnative.connectors:pulsar-flink-connector-${scalaBinaryVersion}:1.12.2.0"

    // add this dependency, if you see the following exception at runtime
    // java.lang.NoClassDefFoundError: Could not initialize class org.apache.pulsar.client.admin.PulsarAdmin
    compile "org.apache.pulsar:pulsar-client-admin:2.7.0"

    // JDBC sink
    flinkShadowJar "org.apache.flink:flink-connector-jdbc_${scalaBinaryVersion}:${flinkVersion}"
    flinkShadowJar "org.postgresql:postgresql:42.2.23"

    // add this dependency, if you see the following exception at runtime
    // java.lang.ClassNotFoundException: org.apache.flink.table.types.DataType
    flinkShadowJar "org.apache.flink:flink-table-common:${flinkVersion}"
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar

    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar

    javadoc.classpath += configurations.flinkShadowJar
}

run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
}
